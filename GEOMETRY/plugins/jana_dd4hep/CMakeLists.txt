
project(jana_dd4hep_project)

# Expose custom cmake modules
# n.b. Some modern versions of cmake will give a warning about the
# the DD4hep_ROOT environment variable being set. You can safely
# ignore the warning. This is set by the thisdd4hep.sh setup script.
# The *new* policy of cmake will be to use this so we would not need
# to add to the CMAKE_MODULE_PATH directly below. The current
# default is to actually ignore the value but print a warning. You
# can change the behavior of your local cmake with the cmake_policy
# command to make it use either the NEW or OLD policy which should
# get rid of the warning.
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake")
list(APPEND CMAKE_MODULE_PATH "$ENV{DD4hep_ROOT}/cmake")

# Set install directory to $JANA_HOME
set(CMAKE_INSTALL_PREFIX $ENV{JANA_HOME} CACHE PATH "magic incantation" FORCE)

# Find dependencies
find_package(JANA REQUIRED)
find_package(ROOT REQUIRED)
find_package(DD4hep REQUIRED)
message(STATUS DD4hep_LIBRARIES=${DD4hep_LIBRARIES})

# DD4hep relies on programatically finding and linking detector-specific
# routines at run time. (It uses strings found in the XML to determine
# symbol names it should load). Those may eventually be placed in a common
# library that is used by both the simulation and reconstruction. This is
# where we will reference the library containing them.
find_library(DETECTOR_LIBS_ECCE ecce PATHS $ENV{EIC_DD4HEP_HOME}/lib)
find_library(DETECTOR_LIBS_IP6 ip6 PATHS $ENV{IP6_DD4HEP_HOME}/lib)
#set(DETECTOR_LIBS ${DETECTOR_LIBS_ECCE} )
set(DETECTOR_LIBS ${DETECTOR_LIBS_ECCE}  ${DETECTOR_LIBS_IP6} )
message(STATUS "DETECTOR_LIBS=${DETECTOR_LIBS}")


# Automatically determine source file list.
file(GLOB mysourcefiles *.cpp *.cc *.c  *.hpp *.hh *.h)
set( jana_dd4hep_PLUGIN_SOURCES ${mysourcefiles} )

# Make plugin target
add_library(jana_dd4hep_plugin SHARED ${jana_dd4hep_PLUGIN_SOURCES})
target_include_directories(jana_dd4hep_plugin PUBLIC ${CMAKE_SOURCE_DIR} ${JANA_INCLUDE_DIR} ${ROOT_INCLUDE_DIRS} ${DD4hep_INCLUDE_DIRS})
target_link_libraries(jana_dd4hep_plugin ${JANA_LIB} ${ROOT_LIBRARIES} ${DD4hep_LIBRARIES} ${DETECTOR_LIBS})
set_target_properties(jana_dd4hep_plugin PROPERTIES PREFIX "" OUTPUT_NAME "jana_dd4hep" SUFFIX ".so")

# The following ensures that on macosx the @rpath search paths go into the plugin
# itself and includes the plugin installation directory. This is needed so
# the ROOT dictionary libraries generated by PODIO are found.
# (n.b. there is probably a better way to do this since I believe this makes
# the plugin non-relocatable as it includes an absolute search path)
set_target_properties(jana_dd4hep_plugin PROPERTIES SKIP_BUILD_RPATH FALSE)
set_target_properties(jana_dd4hep_plugin PROPERTIES BUILD_WITH_INSTALL_RPATH TRUE)
set_target_properties(jana_dd4hep_plugin PROPERTIES INSTALL_RPATH_USE_LINK_PATH TRUE)
set_property(
        TARGET jana_dd4hep_plugin
        PROPERTY INSTALL_RPATH
        "$ENV{EIC_DD4HEP_HOME}/lib"
        "$ENV{IP6_DD4HEP_HOME}/lib"
)
# Install the plugin
install(TARGETS jana_dd4hep_plugin DESTINATION plugins)

# Install headers from this plugin
file(GLOB my_headers "*.h*")
install(FILES ${my_headers} DESTINATION include/jana_dd4hep)


