cmake_minimum_required(VERSION 3.9)
cmake_policy(SET CMP0074 NEW)  # use the policy to look for <package>_ROOT envar

project(dd4hep_project)

# Find dependencies
find_package(JANA REQUIRED)
find_package(ROOT REQUIRED)
find_package(DD4hep REQUIRED)

# DD4hep relies on programatically finding and linking detector-specific
# routines at run time. (It uses strings found in the XML to determine
# symbol names it should load). Those may eventually be placed in a common
# library that is used by both the simulation and reconstruction. This is
# where we will reference the library containing them.
find_library(DETECTOR_LIBS_ECCE ecce PATHS $ENV{EIC_DD4HEP_HOME}/lib)
find_library(DETECTOR_LIBS_IP6 ip6 PATHS $ENV{IP6_DD4HEP_HOME}/lib)
set(DETECTOR_LIBS ${DETECTOR_LIBS_ECCE}  ${DETECTOR_LIBS_IP6} )
message(STATUS "DETECTOR_LIBS=${DETECTOR_LIBS}")

# Source files for plugin only (not library)
set(PLUGIN_SOURCES dd4hep.cc)

# Compile all other sources into a library
file(GLOB LIBRARY_SOURCES *.cpp *.cc *.c  *.hpp *.hh *.h)
list(REMOVE_ITEM LIBRARY_SOURCES ${PLUGIN_SOURCES})

set( INCLUDE_DIRS ${CMAKE_SOURCE_DIR} ${JANA_INCLUDE_DIR} ${ROOT_INCLUDE_DIRS} ${DD4hep_INCLUDE_DIRS})
set( LINK_LIBRARIES ${JANA_LIB} ${ROOT_LIBRARIES} ${DD4hep_LIBRARIES} ${DETECTOR_LIBS})

# Define library
add_library( dd4hep_library STATIC ${LIBRARY_SOURCES} )
target_include_directories( dd4hep_library PUBLIC ${INCLUDE_DIRS} )
target_link_libraries(dd4hep_library ${LINK_LIBRARIES})

# Define plugin
add_library(dd4hep_plugin SHARED ${PLUGIN_SOURCES})
target_include_directories(dd4hep_plugin PUBLIC ${INCLUDE_DIRS})
target_link_libraries(dd4hep_plugin dd4hep_library ${LINK_LIBRARIES})
set_target_properties(dd4hep_plugin PROPERTIES PREFIX "" OUTPUT_NAME "dd4hep" SUFFIX ".so")

# Install plugin
install(TARGETS dd4hep_plugin DESTINATION plugins)

# Install headers for plugin
file(GLOB my_headers "*.h*")
install(FILES ${my_headers} DESTINATION include/services/dd4hep)


# The following ensures that on macosx the @rpath search paths go into the plugin
# itself and includes the plugin installation directory. This is needed so
# the ROOT dictionary libraries generated by PODIO are found.
# (n.b. there is probably a better way to do this since I believe this makes
# the plugin non-relocatable as it includes an absolute search path)
set_target_properties(dd4hep_plugin PROPERTIES SKIP_BUILD_RPATH FALSE)
set_target_properties(dd4hep_plugin PROPERTIES BUILD_WITH_INSTALL_RPATH TRUE)
set_target_properties(dd4hep_plugin PROPERTIES INSTALL_RPATH_USE_LINK_PATH TRUE)
set_property(
        TARGET dd4hep_plugin
        PROPERTY INSTALL_RPATH
        "$ENV{EIC_DD4HEP_HOME}/lib"
        "$ENV{IP6_DD4HEP_HOME}/lib"
)
