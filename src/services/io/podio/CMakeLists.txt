
cmake_minimum_required(VERSION 3.9)
cmake_policy(SET CMP0074 NEW)  # use the policy to look for <package>_ROOT envar

project(jana_edm4hep_project)

# Expose custom cmake modules
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}/cmake")

# Set install directory to $JANA_HOME
set(CMAKE_INSTALL_PREFIX $ENV{JANA_HOME} CACHE PATH "magic incantation" FORCE)

# Find dependencies
find_package(JANA REQUIRED)
find_package(EDM4HEP REQUIRED)
find_package(podio REQUIRED)
set(PODIO_LIBRARIES EDM4HEP::edm4hep EDM4HEP::edm4hepDict podio::podioRootIO)

# This is used to automatically run the make_datamodel_glue.py command
# that generates the datamodel_glue.h file. This is temporary until
# a better mechanism using PODIO tools is developed.
add_custom_command(
        OUTPUT  datamodel_glue.h          # Treated as relative to CMAKE_CURRENT_BINARY_DIR
        COMMAND python ${PROJECT_SOURCE_DIR}/make_datamodel_glue.py
        WORKING_DIRECTORY ${PROJECT_SOURCE_DIR}
        DEPENDS ${PROJECT_SOURCE_DIR}/make_datamodel_glue.py
)

# Automatically determine source file list.
file(GLOB mysourcefiles *.cpp *.cc *.c  *.hpp *.hh *.h)
set( jana_edm4hep_PLUGIN_SOURCES ${mysourcefiles} datamodel_glue.h )

# Make plugin target
add_library(jana_edm4hep_plugin SHARED ${jana_edm4hep_PLUGIN_SOURCES})
target_include_directories(jana_edm4hep_plugin PUBLIC ${CMAKE_SOURCE_DIR} ${PROJECT_SOURCE_DIR} ${PROJECT_BINARY_DIR} ${JANA_INCLUDE_DIR} ${ROOT_INCLUDE_DIRS})
target_link_libraries(jana_edm4hep_plugin ${JANA_LIB} ${PODIO_LIBRARIES})
set_target_properties(jana_edm4hep_plugin PROPERTIES PREFIX "" OUTPUT_NAME "jana_edm4hep" SUFFIX ".so")

# The following ensures that on macosx the @rpath search paths go into the plugin
# itself and includes the plugin installation directory. This is needed so
# the ROOT dictionary libraries generated by PODIO are found.
# (n.b. there is probably a better way to do this since I believe this makes
# the plugin non-relocatable as it includes an absolute search path)
set_target_properties(jana_edm4hep_plugin PROPERTIES SKIP_BUILD_RPATH FALSE)
set_target_properties(jana_edm4hep_plugin PROPERTIES BUILD_WITH_INSTALL_RPATH TRUE)
set_target_properties(jana_edm4hep_plugin PROPERTIES INSTALL_RPATH_USE_LINK_PATH TRUE)
set_target_properties(jana_edm4hep_plugin PROPERTIES INSTALL_RPATH ${CMAKE_INSTALL_PREFIX}/plugins)


# Install the plugin
install(TARGETS jana_edm4hep_plugin DESTINATION plugins)

# Install headers from this plugin and ones generated by PODIO
file(GLOB my_headers "*.h*")
install(FILES ${my_headers} DESTINATION include/jana_edm4hep)
install(FILES ${podio_headers} DESTINATION include/jana_edm4hep)

# Install root dictionaries made by PODIO
set(my_root_dict_files
        ${PROJECT_BINARY_DIR}/libEDM4HEPDataModelDict_rdict.pcm
        ${PROJECT_BINARY_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}EDM4HEPDataModel${CMAKE_SHARED_LIBRARY_SUFFIX}
        ${PROJECT_BINARY_DIR}/${CMAKE_SHARED_LIBRARY_PREFIX}EDM4HEPDataModelDict${CMAKE_SHARED_LIBRARY_SUFFIX})
#install(FILES ${my_root_dict_files} DESTINATION plugins)

